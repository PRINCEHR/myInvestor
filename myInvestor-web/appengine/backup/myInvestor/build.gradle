buildscript {    // Configuration for building
  repositories {
    jcenter()    // Bintray's repository - a fast Maven Central mirror & more
    mavenCentral()
  }
  dependencies {
    classpath 'com.google.cloud.tools:appengine-gradle-plugin:+'    // latest App Engine Gradle tasks
  }
}

repositories {   // repositories for Jar's you access in your code
  maven {
     url 'https://maven-central.storage.googleapis.com'             // Google's mirror of Maven Central
  	 // url 'https://oss.sonatype.org/content/repositories/snapshots'  // SNAPSHOT Repository (if needed)
  }
  jcenter()
  mavenCentral()
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.5.1'
}

// def projectId = 'myinvestor-prod'
def projectId = 'myinvestor-stg'

apply plugin: 'eclipse'
apply plugin: 'java'                              // standard Java tasks
apply plugin: 'war'                               // standard Web Archive plugin
apply plugin: 'com.google.cloud.tools.appengine'  // App Engine tasks

dependencies {
   
  providedCompile group: 'javax.servlet', name: 'servlet-api', version:'2.5'
  
  // Lombok
  compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.16.18'
  
  // AppEngine SDK
  // compile 'com.google.appengine:appengine:+'
  compile group: 'com.google.appengine', name: 'appengine-api-1.0-sdk', version: APPENGINE_SDK_VERSION
  compile group: 'com.google.appengine', name: 'appengine-api-labs', version: APPENGINE_SDK_VERSION
  
  // Other dependencies
  compile group: 'com.googlecode.objectify', name: 'objectify', version: OBJECTIFY_VERSION
  compile group: 'com.google.guava', name: 'guava', version: GUAVA_VERSION
  compile group: 'com.google.inject', name: 'guice', version: GUICE_VERSION
  compile group: 'com.google.inject.extensions', name: 'guice-servlet', version: GUICE_VERSION
  compile group: 'org.apache.commons', name: 'commons-lang3', version: COMMON_LANG_VERSION
  compile group: 'org.slf4j', name: 'slf4j-api', version: SLF4J_VERSION
  compile group: 'org.slf4j', name: 'slf4j-jdk14', version: SLF4J_VERSION
  compile group: 'com.sun.jersey', name: 'jersey-server', version: JERSEY_VERSION
  compile group: 'com.sun.jersey', name: 'jersey-servlet', version: JERSEY_VERSION
  compile group: 'com.sun.jersey.contribs', name: 'jersey-guice', version: JERSEY_VERSION
  compile group: 'com.fasterxml.jackson.jaxrs', name: 'jackson-jaxrs-json-provider', version: JACKSON_VERSION
  

}

// [App Engine start]
appengine {  					// App Engine tasks configuration
  run {      					// local (dev_appserver) configuration (standard environments only)
    port = 8080                 // default
    //jvmFlags = ['-Xdebug', '-Xrunjdwp:transport=dt_socket,server=y,suspend=y,address=5005', "-Dappengine.fullscan.seconds=5"]
  }

  deploy {   // deploy configuration
    stopPreviousVersion = true  // default - stop the current version
    promote = true              // default - & make this the current version
  }
}
// [App Engine end]

group = 'com.myinvestor'   // Generated output GroupId
version = '0.1.0'          // Version in generated output

sourceCompatibility = 1.7  // App Engine Standard uses Java 7
targetCompatibility = 1.7  // App Engine Standard uses Java 7


task reloadApp(type: Copy) {
  dependsOn war

  project.afterEvaluate {
    into project.tasks.explodeWar.explodedAppDirectory
    with war
  }
}

// this replaces the ${endpoints.project.id} in appengine-web.xml and web.xml
task replaceProjectId(type: Copy) {
    from 'src/main/webapp/WEB-INF/'
    include '*.xml'
    into 'build/exploded-myInvestor/WEB-INF'		// Changed to the project name
    expand(endpoints:[project:[id:projectId]])
    filteringCharset = 'UTF-8'
}
assemble.dependsOn replaceProjectId


buildscript {    // Configuration for building
  repositories {
    jcenter()    // Bintray's repository - a fast Maven Central mirror & more
    mavenCentral()
  }
  dependencies {
    classpath 'com.google.cloud.tools:endpoints-framework-gradle-plugin:+'
    classpath 'com.google.cloud.tools:appengine-gradle-plugin:+'    // latest App Engine Gradle tasks
  }
}

repositories {   // repositories for Jar's you access in your code
  maven {
     url 'https://maven-central.storage.googleapis.com'             // Google's mirror of Maven Central
  	 // url 'https://oss.sonatype.org/content/repositories/snapshots'  // SNAPSHOT Repository (if needed)
  }
  jcenter()
  mavenCentral()
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.0.1'
}

def projectId = 'myInvestor'

apply plugin: 'eclipse'
apply plugin: 'java'                              // standard Java tasks
apply plugin: 'war'                               // standard Web Archive plugin
apply plugin: 'com.google.cloud.tools.appengine'  // App Engine tasks
apply plugin: 'com.google.cloud.tools.endpoints-framework-server'

dependencies {
   
  providedCompile group: 'javax.servlet', name: 'servlet-api', version:'2.5'
  compile 'com.google.appengine:appengine:+'
  
  // Google Cloud EndPoint framework
   compile 'com.google.endpoints:endpoints-framework:+'
   compile 'com.google.endpoints:endpoints-management-control-appengine:+'
   compile 'com.google.endpoints:endpoints-framework-auth:+'
    
  // myInvestor dependencies
  compile group: 'com.google.guava', name: 'guava', version: '11.0.2'
  compile group: 'com.google.appengine', name: 'appengine-api-1.0-sdk', version: APPENGINE_SDK_VERSION

}


// [App Engine start]
appengine {  					// App Engine tasks configuration
  run {      					// local (dev_appserver) configuration (standard environments only)
    port = 8080                 // default
    //jvmFlags = ['-Xdebug', '-Xrunjdwp:transport=dt_socket,server=y,suspend=y,address=5005', "-Dappengine.fullscan.seconds=5"]
  }

  deploy {   // deploy configuration
    stopPreviousVersion = true  // default - stop the current version
    promote = true              // default - & make this the current version
  }
}
// [App Engine end]

group = 'com.myinvestor'   // Generated output GroupId
version = '0.1.0'          // Version in generated output

endpointsServer {
  // Endpoints Framework Plugin server-side configuration
  hostname = "echo-api.endpoints.${projectId}.cloud.goog"
}

sourceCompatibility = 1.7  // App Engine Standard uses Java 7
targetCompatibility = 1.7  // App Engine Standard uses Java 7


task reloadApp(type: Copy) {
  dependsOn war

  project.afterEvaluate {
    into project.tasks.explodeWar.explodedAppDirectory
     with war
  }
}

// this replaces the ${endpoints.project.id} in appengine-web.xml and web.xml
task replaceProjectId(type: Copy) {
    from 'src/main/webapp/WEB-INF/'
    include '*.xml'
    into 'build/exploded-backend/WEB-INF'
    expand(endpoints:[project:[id:projectId]])
    filteringCharset = 'UTF-8'
}
assemble.dependsOn replaceProjectId

